PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX olex: <http://www.w3.org/ns/lemon/ontolex#>
PREFIX afn: <http://jena.apache.org/ARQ/function#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX lcc-lr: <https://www.omg.org/spec/LCC/Languages/LanguageRepresentation/>


CONSTRUCT {
  ?X a ?Y;
    owl:sameAs ?Alias;

    rdfs:label ?lab;
    rdfs:comment ?comm;

    rdfs:isDefinedBy ?definedBy;

    skos:notation ?notation;
    skos:prefLabel ?pLab;
    skos:altLabel ?aLab;
    skos:hiddenLabel ?hLab;
    skos:definition ?def;
    skos:note ?note;
    skos:example ?example;

    rdfs:subClassOf ?parentType;
    rdfs:subPropertyOf ?parentProp;

    owl:versionInfo ?VersionTag;
    dc:identifier ?identifier.

  ?Dx a rdfs:Datatype.
}
WHERE {
    ?X a ?Y

        OPTIONAL { ?X rdfs:subClassOf ?parentType.
             FILTER isUri( ?parentType ).
        }

        OPTIONAL { ?X rdfs:subPropertyOf ?parentProp.
             FILTER isUri( ?parentProp ).
        }

        OPTIONAL { ?X owl:sameAs ?Alias. }

        OPTIONAL { ?X rdfs:label ?lab. }
        OPTIONAL { ?X rdfs:comment ?comm. }
        OPTIONAL { ?X rdfs:isDefinedBy ?isDefinedBy. }

        OPTIONAL { ?X skos:prefLabel ?pLab. }
        OPTIONAL { ?X skos:altLabel ?aLab. }
        OPTIONAL { ?X skos:hiddenLabel ?hLab. }
        OPTIONAL { ?X skos:definition ?def. }
        OPTIONAL { ?X skos:note ?note. }
        OPTIONAL { ?X skos:example ?example. }

        OPTIONAL { ?X dct:identifier ?dctId. }
        OPTIONAL { ?X dc:identifier ?dcId. }

        OPTIONAL { ?X skos:notation ?N. }
        OPTIONAL { ?X owl:versionInfo ?VersionTag. }
        OPTIONAL { ?Code lcc-lr:identifies ?X.
                   OPTIONAL {
                     ?Code rdf:type ?Z.
                     FILTER( afn:namespace(?Z) != "http://www.w3.org/2002/07/owl#" ). }
                   ?Code lcc-lr:hasTag ?Tag. }

        BIND ( URI( CONCAT( "urn:", afn:localname(?Z) ) ) as ?Dx ).

        BIND ( STRDT( STR( ?Tag ), ?Dx ) as ?TTag ).
        BIND ( COALESCE( ?dcId, ?dctId ) as ?identifier ).
        BIND ( COALESCE( ?N, ?TTag, ?Tag ) as ?notation ).
        BIND ( COALESCE( ?isDefinedBy, ?baseUri ) as ?definedBy ).
}