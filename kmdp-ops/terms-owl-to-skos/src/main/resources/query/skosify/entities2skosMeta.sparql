PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX olex: <http://www.w3.org/ns/lemon/ontolex#>
PREFIX dce: <http://www.opengroup.org/dce/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX lcc-lr: <https://www.omg.org/spec/LCC/Languages/LanguageRepresentation/>
PREFIX f: <http://kmdp.mayo.edu/sparql/function#>
PREFIX afn: <http://jena.apache.org/ARQ/function#>

CONSTRUCT {
  ?Cid

    rdfs:label ?Label;
    rdfs:comment ?Comment;

    skos:notation ?Notation;

    skos:prefLabel ?PrefLabel;
    skos:altLabel ?AltLabel;
    skos:hiddenLabel ?HiddenLabel;

    skos:definition ?Def;
    skos:note ?Note;
    skos:example ?Example;

    owl:versionInfo ?VersionTag;
    dc:identifier ?Id.

}
WHERE {

    VALUES ?entity { owl:Class owl:ObjectProperty owl:DatatypeProperty owl:NamedIndividual }

    ?X a ?entity.
        FILTER isUri( ?X ).

            # Create a URI for the related concept
            OPTIONAL { ?X dc:identifier ?dcId. }
            OPTIONAL { ?X dct:identifier ?dctId. }
            OPTIONAL { ?X skos:notation ?N. }
            OPTIONAL { ?Code lcc-lr:identifies ?X. ?Code lcc-lr:hasTag ?Tag. }
            OPTIONAL { ?X owl:versionInfo ?VersionTag. }
              BIND( COALESCE( ?VersionTag, "" ) as ?VTag ).

            BIND ( COALESCE( ?dcId, ?dctId, f:localName(?X) ) as ?LocalId )
            BIND ( f:uuidFrom( ?LocalId, ?VTag ) as ?Id )
            BIND ( COALESCE( ?N, ?Tag, ?LocalId ) as ?Notation )
            BIND ( URI( CONCAT( STR( tgt: ), ?Id ) ) as ?Cid ).

        #For each proper ontology entity
        BIND( f:localName( ?X ) as ?localName )

        OPTIONAL { ?X rdfs:label ?lab. }
        OPTIONAL { ?X skos:prefLabel ?pLab. }
        OPTIONAL { ?X skos:altLabel ?AltLabel. }
        OPTIONAL { ?X skos:hiddenLabel ?HiddenLabel. }

        OPTIONAL { ?X skos:definition ?Def. }
        OPTIONAL { ?X skos:example ?Example. }
        OPTIONAL { ?X skos:note ?Note. }

        OPTIONAL { ?X rdfs:comment ?Comment. }

        BIND ( COALESCE( ?lab, ?pLab, ?localName ) as ?Label ).
        BIND ( COALESCE( ?pLab, ?lab, ?localName ) as ?PrefLabel ).
}
