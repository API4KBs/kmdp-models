PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX olex: <http://www.w3.org/ns/lemon/ontolex#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX lcc-lr: <http://www.omg.org/spec/LCC/Languages/LanguageRepresentation/>
PREFIX afn: <http://jena.apache.org/ARQ/function#>
PREFIX f: <http://kmdp.mayo.edu/sparql/function#>


CONSTRUCT {

  ?Cid
    a owl:NamedIndividual;
    rdf:type skos:Concept;
    rdfs:isDefinedBy ?X;

    skos:inScheme ?Scheme;

    skos:broader ?Parent;
    skos:broaderTransitive ?Top;
    skos:broader ?Cid.

  ?Parent a skos:Concept.

  ?Scheme
    a skos:ConceptScheme;
    a owl:NamedIndividual;
    rdfs:label ?SchemeLabel;
    skos:prefLabel ?SchemeLabel;
    skos:hasTopConcept ?Top.

  ?Top a skos:Concept;
    rdfs:label ?TopLabel;
    skos:prefLabel ?TopLabel.

}
WHERE {
    VALUES ?entity { owl:Class owl:ObjectProperty owl:DatatypeProperty owl:NamedIndividual }

    #For each proper ontology entity
    OPTIONAL {

        ?X a ?entity.
            FILTER isUri( ?X ).

            # Create a URI for the related concept
            OPTIONAL { ?X dc:identifier ?dcId. }
            OPTIONAL { ?X dct:identifier ?dctId. }
            OPTIONAL { ?Code lcc-lr:identifies ?X. ?Code lcc-lr:hasTag ?Tag. }
            BIND ( URI( CONCAT( STR( tgt: ),
                            f:uuidFrom( COALESCE( ?dcId, ?dctId, ?Tag, f:localName(?X) ) ) ) ) as ?Cid ).

        BIND ( tgt: as ?Namespace ).
        BIND ( STRLEN( STR( ?Namespace ) ) as ?Len ).
        BIND ( COALESCE( ?schemeName,
                          CONCAT (afn:localname( URI( SUBSTR( STR( ?Namespace ), 1, ?Len - 1 ) ) ),
                                           "_Scheme" ) ) as ?SchemeLabel )
        BIND ( URI( CONCAT( STR( tgt: ),
                            f:uuidFrom( ?SchemeLabel ) ) )
               as ?Scheme ).

        OPTIONAL {
                    ?X rdfs:subClassOf ?Y;
                    OPTIONAL { ?Y dc:identifier ?dcId2. }
                    OPTIONAL { ?Y dct:identifier ?dctId2. }
                    OPTIONAL { ?Code2 lcc-lr:identifies ?Y. ?Code2 lcc-lr:hasTag ?Tag2. }
                    BIND ( URI( CONCAT( STR( tgt: ),
                                  f:uuidFrom( COALESCE( ?dcId2, ?dctId2, ?Tag2, f:localName(?Y) ) ) ) ) as ?ClassParent ).
                    FILTER( afn:namespace(?Y) != "http://www.w3.org/2002/07/owl#" ).
                }
        OPTIONAL {
                    ?X rdf:type ?Y;
                    OPTIONAL { ?Y dc:identifier ?dcId3. }
                    OPTIONAL { ?Y dct:identifier ?dctId3. }
                    OPTIONAL { ?Code3 lcc-lr:identifies ?Y. ?Code3 lcc-lr:hasTag ?Tag3. }
                    BIND ( URI( CONCAT( STR( tgt: ),
                                  f:uuidFrom( COALESCE( ?dcId3, ?dctId3, ?Tag3, f:localName(?Y) ) ) ) ) as ?InstanceParent ).
                    FILTER( afn:namespace(?Y) != "http://www.w3.org/2002/07/owl#" ).
                }
        OPTIONAL {
                    ?X rdfs:subPropertyOf ?Y;
                    OPTIONAL { ?Y dc:identifier ?dcId4. }
                    OPTIONAL { ?Y dct:identifier ?dctId4. }
                    OPTIONAL { ?Code4 lcc-lr:identifies ?Y. ?Code4 lcc-lr:hasTag ?Tag4. }
                    BIND ( URI( CONCAT( STR( tgt: ),
                                  f:uuidFrom( COALESCE( ?dcId4, ?dctId4, ?Tag4, f:localName(?Y) ) ) ) ) as ?PropertyParent ).
                    FILTER( afn:namespace(?Y) != "http://www.w3.org/2002/07/owl#" ).
                }



        BIND ( URI( CONCAT( STR( tgt: ),
                COALESCE( ?topName, CONCAT( afn:localname( ?Scheme ), "_Top" ) ) ) ) as ?Top ).

        BIND ( COALESCE( ?ClassParent,
                         ?InstanceParent,
                         ?PropertyParent,
                         ?Top ) as ?Parent ).

        BIND ( afn:localname( ?Top ) as ?TopLabel ).


    }

}




